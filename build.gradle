plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'pmd'
	id "com.github.spotbugs" version "4.3.0"
	id 'jacoco'
}

repositories {
	mavenCentral()
}

//Settings
bootJar {
	archiveFileName = 'discovery-service.jar'
}

group = 'com.ecommerce'
sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

ext {
	set('springCloudVersion', "Hoxton.SR8")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

checkstyle {
	configFile = file('codequality/checkstyle/checkstyle.xml')
}
tasks.withType(Checkstyle) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

// configure HTML report
spotbugsMain {
	reports {
		html {
			enabled = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			enabled = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}


jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled = true
		html.destination file("${buildDir}/reports/jacoco")
	}
}


jacocoTestReport {
	dependsOn test
}

dependencies {
	//Eureka & Actuator
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	//Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

